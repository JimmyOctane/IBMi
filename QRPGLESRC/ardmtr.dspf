     A*%%TS  SD  20260213  153415  JFLANARY    REL-V7R5M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LBAFREF)
     A                                      CHGINPDFT(UL)
     A                                      INDARA
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A N33                                  PAGEDOWN
     A                                      PAGEUP
     A                                      PRINT
     A*=============================================================
     A*  Subfile number one
     A*=============================================================
     A          R SUB01                     SFL
     A*%%TS  SD  20260213  153415  JFLANARY    REL-V7R5M0  5770-WDS
     A                                      SFLNXTCHG
     A            S1PLAN         2  0O  8  3
     A            S1DIVISION     3   O  8  6
     A            S1SECTION      3   O  8 10
     A            S1DOLLARL      7Y 0O  8 15EDTCDE(2)
     A            S1DOLLARH      7Y 0O  8 26EDTCDE(2)
     A            S1PRECENTL     3  1O  8 38EDTWRD('  . ')
     A            S1PERCENTH     3  1O  8 44EDTWRD('  . ')
     A            S1COMMRATE     4  2O  8 50EDTWRD('  .  ')
     A*=============================================================
     A*  Control for subfile number one
     A*=============================================================
     A          R SUB01CTL                  SFLCTL(SUB01)
     A*%%TS  SD  20260213  153415  JFLANARY    REL-V7R5M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0005)
     A                                      WINDOW(FMT1)
     A                                      RTNCSRLOC(&#REC &#FLD)
     A                                      BLINK
     A                                      OVERLAY
     A                                      SFLCSRRRN(&WHERE)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  33                                  SFLEND(*MORE)
     A            SCRRN          4S 0H      SFLRCDNBR
     A            #REC          10A  H
     A            #FLD          10A  H
     A            WHERE          5S 0H
     A            C1TITLE       50A  O  1  8DSPATR(HI)
     A                                  2  2'SUB01CTL'
     A                                      COLOR(BLU)
     A                                  2 54USER
     A                                      COLOR(BLU)
     A                                  3 54DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  7  3'Pl'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7  6'Div'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 10'Sec'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 15'Low      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 26'High     '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 38'Low '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 44'High'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 50'Rate'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  6 50'Comm'
     A                                      DSPATR(HI)
     A                                  6 38'-Profit%-'
     A                                      DSPATR(HI)
     A                                  6 15' ------ Sales ------'
     A                                      DSPATR(HI)
     A*=============================================================
     A          R FMT1
     A*%%TS  SD  20250805  105043  JFLANARY    REL-V7R5M0  5770-WDS
     A                                      WINDOW(5 10 15 64 *NOMSGLIN)
     A                                      RMVWDW
     A                                 14  2'F3=Exit'
     A                                      COLOR(BLU)
      *=========================================================================
     A          R MSGSFL                    SFL
     A                                      SFLMSGRCD(12)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ(10)
     A*=========================================================================
     A* Message subfile stuff
     A*=========================================================================
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A*%%TS  SD  20140618  143904  XJF90405    REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A                                      WINDOW(FMT1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N03                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            PGMQ                      SFLPGMQ(10)
     A*=============================================================
     A          R DUMMY
     A                                      ASSUME
     A                                  2  3' '
